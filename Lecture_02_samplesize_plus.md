# サンプルサイズ設計のシミュレーション
サンプルサイズは、α・β・効果量の3つの数値を決定すると計算することができます。今回は、アウトカムが連続量であり、それらの平均値の2群間比較を行うという設定で考えます。しかし、この方法で計算されたサンプルサイズで本当に大丈夫なのか確認したいと思います。
## 事例1：平均値の2群比較
### Stataによる数値計算

事例として下記の様な状況を想定します。
対照薬群：平均値=1.3、標準偏差=1.0
治療薬群：平均値=2.0、標準偏差=1.5
この平均値差0.7を有意性をもって検出するために必要なサンプルサイズがいくらであるかを検討します。

Stataでのコマンドは下記です。

```
power twomeans 1.3 2.0, sd1(1.0) sd2(1.5)
```

Stataにおいて、サンプルサイズを計算するコマンドは`power`です。サブコマンドでどのような状況でのサンプルサイズ設計であるかを指定します。ここでは、2群間の平均値比較なので、`twomeans`を指定しています。その後に、対照薬群の平均値と治療薬群の平均値を並記し、オプションとしてそれぞれの標準偏差を`sd1()`と`sd2()`として指定しています。αとβは`a()`と`b()`オプションで指定することができますが、指定が無い場合はα=0.05およびβ=0.2が採用されます。

上記のコマンドにより、この状況で必要なサンプルサイズは片群54症例（両群108症例）と算出されます。

さて、これで良いでしょうか？

### シミュレーションによる確認
片群54症例で想定通りの結果が得られるかどうか確認します。

Stataコマンドを下記の様に作成すると、確認できます。

```
clear
set obs 108
set seed 12345

gen intv = 0 in 1/54
replace intv = 1 in 55/l

gen      x = rnormal(1.3, 1.0) if intv==0
replace x = rnormal(2.0, 1.5) if intv==1

ttest x, by(intv) welch
```

このコードの解説をします。まず、このコードの最初の3行では、シミュレーションの準備をおこなっています。`clear`でデータセットがあれば、それを消去します。`set obs 108`で新たに作るシミュレーション用のデータセットの症例数を108にしました。最後の`set seed 12345`は、シミュレーションで用いられる乱数のシード値を固定します。12345のところはどんな数値でも構いません。

次の2行では、シミュレーション用のデータセットにおける対照薬群と治療薬群を示す変数を設定しています。intv=0が対照薬群でintv=1が治療薬群です。それぞれ54症例になるように、`in 1/54`と`in 55/l`で設定しています。

3番目のブロックの2行は、対照薬群と治療薬群について、アウトカムの値を乱数で生成しています。乱数の設定には`rnormal()`関数を用いています。この関数は、指定した平均とと標準偏差をもつ乱数を発生させます。ここでは、サンプルサイズ設計に用いた平均値と標準偏差が指定され、変数`x`に代入されています。

下記のコマンドは`rnormal()`関数で生成した乱数を`display`コマンドで表示させている例です。このコマンドは実行毎に異なる数値を返しますが、平均値1.3で標準偏差が1.0となります。

```
di rnormal(1.3, 1.0)
```

最後に`ttest`でウェルチのt検定を行っています。

このt検定の結果が、シミュレーションによる検定結果でした。有意になったかどうか確認します。

![image](https://github.com/sankyoh/Stata_Lecture2023/assets/67684585/e8392f9d-9841-4af6-aeb3-333d964833ff)

残念ながら、p=0.0585であり、有意な結果にはなりませんでした。

また、群間の平均値の差は0.7を想定していましたが、実際は0.47程度でした。乱数で外れを引いたようです。

### シミュレーションによる確認・続き
では、算出した片群54症例（両群108症例）では、足りないのでしょうか？

今回は、シミュレーション1回のみだったので、そこまで確実なことが言えません。もう何回か繰り返してみましょう。

繰り返すときに、乱数シードを変更しないと、全く同じ乱数は発生して、同一の結果になってしまいます。ですので、先ほどのコードから乱数のシード値だけを変更して実行します。

例えば、先ほどのコードで乱数シードを指定した箇所を`set seed 1234567`にします。

![image](https://github.com/sankyoh/Stata_Lecture2023/assets/67684585/45dcb041-f2fb-4843-a669-fdbe6b55b188)

この条件で実行すると、p<0.0001となり、有意な結果となりました。

当たり前ですが、有意性検定はサンプリングされるデータによって、有意になったり、ならなかったりします。

では、どのくらい「有意」という結果が得られたらよいかと言うと、**統計学的検出力の分**だけです。

統計学的検出力は、1-βで計算されますので、今回は0.8（80%）です。つまり、100回検定をおこなったら、80回は平均値1.3と平均値2.0の差0.7を検出し、「有意である」という結果がほしいのです。

ここまでで、シード値を変えて、2回のシミュレーションを実行しました。うち1回が有意でしたので、50％の検出です。

しかし、当然2回のみの試行ですので、何とも言えません。

## シミュレーションによる確認・続き2
2回の試行では到底足りませんので、試しに1000回くらい繰り返し処理を行います。繰り返しには、`forvalues`を用います。

```
set seed 12345
forvalues i=1/1000{
	clear
	set obs 108
	
	gen intv = 0 in 1/54
	replace intv = 1 in 55/l
	
	gen      x = rnormal(1.3, 1.0) if intv==0
	replace x = rnormal(2.0, 1.5) if intv==1
	
	ttest x, by(intv) welch
}
```

これで、1000回繰り返しを行いました。`set seed 12345`がループの外にあることに注意してください。

QUIZ：forvaluesの中に`set seed 12345`があると、どのような結果になるでしょうか？

1000回を行ったのですが、そのうち何回が有意性を示せたのかわかりません。凄い勢いで流れていきましたので、確認する事ができません。1000回分の結果を目で確認しながら記録することも現実的ではありません。

シミュレーションの結果を記録する仕組みが必要です。

### シミュレーションの結果を記録する
シミュレーションの結果を記録する方法として、`frame`を用いる方法と`matrix`を使う方法があります。今回は後で`summarize`コマンドで集計をしようと思いますので、`frame`を使います。

`frame`という仕組みは、複数のデータセットを扱えないというStataの欠点を補うものです。

フレームはデータフレームとも呼ばれ、複数のデータセットを同時にメモリに格納することができます。メモリ上のデータセットはフレームに格納され、Stataでは複数のフレームを使用することができます。複数のフレームを切り替えることができ、フレーム内のデータを他のフレームのデータにリンクすることもできます。詳細については、`help frames intro`を実行して下さい。

今回は、シミュレーション用フレームと結果格納フレームの2つを使います。それぞれのフレーム名を`defualt`と`results`とします。

`default`フレームは、最初からあるフレームですので、新規に作成する必要はありません。いつも意識せずに使っているフレームが`default`フレームです。さっき、シミュレーションを行ったのも`default`フレームの中で行っていました。

#### フレームの作成

`results`フレームは、新規に作成する必要があります。下記のコマンドで作成ます。

```
capture frame create results
frame change results
	clear
	set obs 1000
	gen pv  = .
	gen pow = .
frame change default
```

まず、1行目ですが`results`という名称のフレームが既にあると新たに同名のファイルを作る事ができませんので、頭に`capture`を付けています。

これにより、既に`results`フレームがある場合は何も起こりません。未だ`results`フレームが無い場合には、新たに作られます。どちらに転んでも、手元に`results`フレームが残ります。

2行目で、操作するフレームを`results`に変更します。これは、`frame change <フレーム名>`で実行可能です。

`clear`から`gen pow = .`までの4行は、`results`フレームに対する操作です。

`clear`コマンドで一旦、フレーム内をクリアします。そして、`set obs 1000`で観察数を設定します。ここの`1000`は、1000回シミュレーションしてその結果を入れるという意味です。

新たに変数`pv`と`pow`を作成していますが、`pv`には個々のシミュレーションでのp値を格納し、`pow`には有意かどうかを示す0または1を格納する予定です。

最後に、`frame change default`で元フレームに戻ります。

#### 検定結果をフレームに格納する

次に、t検定を実行した後に、そのp値と有意だったかどうかを`results`フレームに格納する部分を作成します。

```
frame change results
  qui{
  replace pv  = r(p) in `i'
  replace pow = 1 if pv <  0.05 in `i'
  replace pow = 0 if pv >= 0.05 in `i'
  }
frame change default
```

t検定は、`default`フレームで行われていますので、格納はフレームを変える必要があります。そのため、最初に`frame change results`を行っています。

次の`qui{}`は、代入の度にStataのレスポンスが出るのが五月蠅いので、静かに実行して貰うためのものです。シミュレーションの本質とは関係ありません。

``replace pv = r(p) in `i'``では、t検定（`ttest`）で得られたp値を変数`pv`に代入しています。p値は、ローカルマクロ`r(p)`で取得しています。この取得については、`help ttest`で確認して下さい。

代入箇所は、``in `i'``で指定しています。このローカルマクロ`` `i' ``は、`forvalues`で指定したものです。何回目の繰り返しかを格納しています。

``replace pow = 1 if pv <  0.05 in `i'``も同様で、変数`pow`に有意であったかどうかを代入しています。

このような操作を一通り行ったら、`frame change default`で元のフレームに戻ります。

### これらを組み合わせる

前節までで、必要なパーツが出そろいました。これらを組み合わせて見ましょう。

```
set seed 12345
******
* フレームの作成
******
capture frame create results
frame change results
	clear
	set obs 1000
	gen pv  = .
	gen pow = .
frame change default

******
* Simulation
******
forvalues i=1/1000 {
	qui{
	clear
	set obs 108
	gen     intv = 0 in 1/54
	replace intv = 1 if intv==.
	gen     x = rnormal(1.3, 1.0) if intv==0
	replace x = rnormal(2.0, 1.5) if intv==1
	ttest x, by(intv) welch
	}
	
******
* 検定結果をフレームに格納する
******
	frame change results
		qui{
		replace pv  = r(p) in `i'
		replace pow = 1 if pv <  0.05 in `i'
		replace pow = 0 if pv >= 0.05 in `i'
		}
	frame change default
}
```

このコードが一通り動いても、おそらく何も表示されないと思います。結果を表示させるところを作っていませんので。

最後に下記コマンドを実行します。

```
frame change results
su
```

![image](https://github.com/sankyoh/Stata_Lecture2023/assets/67684585/52ca8468-347e-4a3b-ae22-42c449524076)


このうち変数`pow`の平均値に着目してください。変数`pow`は、有意であれば「1」なので、平均値が0.812ということは、1000回中822回は有意になったということです。つまり、検出力が0.812であるということに相当します。

めでたく、検出力0.8を超えることができましたので、片群54症例（両群108症例）でOKでしょうか。

念のため、10,000回のシミュレーションを行ってみましょう。下記の様な結果になったはずです。

![image](https://github.com/sankyoh/Stata_Lecture2023/assets/67684585/02b99f39-60ac-4de8-8e44-b23c6d9cd71f)

さすがに10,000回やっても0.8を超えているので、このサンプルサイズは「設定された条件の元であれば」、大丈夫な数と言えるでしょう。

もしも、駄目だったときには、0.8を超えるまでサンプルサイズを少し増やしてシミュレーションします（グリッドサーチ）。

### QUIZ
下記の条件下で必要なサンプルサイズを数値計算で設計し、その数値をシミュレーションで確認して下さい。
* 対照群＝平均値12.5(SD=4.5)
* 介入群＝平均値15.5(SD=3.1)

### doファイル化
この内容を毎回毎回行うのも面倒なので、doファイルにまとめておきます。

この時に、シミュレーションによって変化する値については、ファイルの冒頭にローカルマクロとして設定できるようにすると便利です。

可変値は下記があります。
* 介入群の平均値
* 介入群の標準偏差
* 対照群の平均値
* 対照群の標準偏差
* αの値（だいたい0.05ですが…）
* 計算されたサンプルサイズ

これらをdoファイルの冒頭で設定するような様式になっています。githubサイトにアップロードしています。

# 時間が余ったとき…
[なぜプログラミングが必要なのか？](https://github.com/sankyoh/Stata_Lecture2022/blob/main/Lecture_01_introduction.md#%E3%81%AA%E3%81%9C%E3%83%97%E3%83%AD%E3%82%B0%E3%83%A9%E3%83%9F%E3%83%B3%E3%82%B0%E3%81%8C%E5%BF%85%E8%A6%81%E3%81%AA%E3%81%AE%E3%81%8B)


